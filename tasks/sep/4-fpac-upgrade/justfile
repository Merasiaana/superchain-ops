set dotenv-load

export rpcUrl := env_var('ETH_RPC_URL')
export signatures := env_var_or_default('SIGNATURES', '')
export bundle := "input"
export location := `pwd`
export OWNER_SAFE := env_var('PROXY_ADMIN_OWNER')

# Default recipe to list all available recipes
default:
  just --list

# Generates the `input.json` file from the addresses in the `meta.json` file.
generate-input:
  #!/usr/bin/env bash
  PORTAL_PROXY=$(jq -r .optimismPortalProxyAddr ./meta.json)
  PORTAL2_IMPL=$(jq -r .optimismPortal2ImplAddr ./meta.json)
  SYSTEM_CONFIG=$(jq -r .systemConfigAddr ./meta.json)
  SUPERCHAIN_CONFIG=$(jq -r .superchainConfigAddr ./meta.json)
  DGF_PROXY=$(jq -r .disputeGameFactoryProxyAddr ./meta.json)

  INITIALIZE_SIG="initialize(address,address,address)"
  INITIALIZE_ARGS=$(cast abi-encode $INITIALIZE_SIG $DGF_PROXY $SYSTEM_CONFIG $SUPERCHAIN_CONFIG)
  ENCODED_INITIALIZE="$(cast sig $INITIALIZE_SIG)${INITIALIZE_ARGS:2}"

  UPGRADE_TO_AND_CALL_SIG="upgradeToAndCall(address,bytes)"
  UPGRADE_TO_AND_CALL_ARGS=$(cast abi-encode $UPGRADE_TO_AND_CALL_SIG $PORTAL2_IMPL $ENCODED_INITIALIZE)
  ENCODED_UPGRADE_TO_AND_CALL="$(cast sig $UPGRADE_TO_AND_CALL_SIG)${UPGRADE_TO_AND_CALL_ARGS:2}"

  cp ./input-template.json ./input.json
  jq "(.transactions[0].to = \"$PORTAL_PROXY\") |
           (.transactions[0].data = \"$ENCODED_UPGRADE_TO_AND_CALL\") |
           (.transactions[0].contractInputsValues._implementation = \"$PORTAL2_IMPL\") |
           (.transactions[0].contractInputsValues._data = \"$ENCODED_INITIALIZE\")" ./input.json > ./input.tmp.json && mv ./input.tmp.json ./input.json

# Simulates the signing of the `OptimismPortal` proxy upgrade transaction.
simulate hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  echo "getting signer address..."
  if [ t"$SIMULATE_WITHOUT_LEDGER" == t1 ]
  then
    signer="0xEA96f8d33Af98839C20547D970EE0961f8865009"
  else
    signer=$(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  fi
  echo "Simulating with: ${signer}"
  echo ""
  forge build
  forge script SignFromJson \
    --rpc-url ${rpcUrl} \
    --sig "signJson(string)" \
    "${location}/${bundle}.json" \
    --sender ${signer}

# Signs the `OptimismPortal` proxy upgrade transaction.
sign hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  echo "getting signer address..."
  signer=$(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  echo "Signing with: ${signer}"
  echo ""
  forge build
  $(git rev-parse --show-toplevel)/bin/eip712sign --ledger --hd-paths "m/44'/60'/{{hdPath}}'/0/0" -- \
  forge script SignFromJson \
    --rpc-url ${rpcUrl} \
    --sig "signJson(string)" \
    "${location}/${bundle}.json"

# Executes the `OptimismPortal` proxy upgrade transaction.
execute hdPath='0':
  #!/usr/bin/env bash
  cd ../..
  sender=$(cast wallet address --ledger --mnemonic-derivation-path "m/44'/60'/{{hdPath}}'/0/0")
  forge build
  forge script --fork-url ${ETH_RPC_URL} SignFromJson \
    --sig "runJson(string,bytes)" "${location}/${bundle}.json" ${SIGNATURES} \
    --ledger --hd-paths "m/44'/60'/{{hdPath}}'/0/0" --broadcast \
    --sender ${sender}
